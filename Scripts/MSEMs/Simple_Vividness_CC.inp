!MSEM for Vividness with CC, 12/29/2022
title: Simple model looking at Vividness with CC;

DATA: FILE = "df_MPlus_12_29.dat";
VARIABLE: 
NAMES = ID trial DERS Avo AvoS LNB_MEFF VP_Pos VP_Neg Rich_Pos Rich_Neg Det_Pos
     Det_Neg Viv_Pos Viv_Neg Int_Pos Int_Neg Ext_Pos Ext_Neg DERS_sc Avo_sc LNB_sc
     DERS_sq Avo_sq LNB_sq; 
MISSING=.;
USEVARIABLES = ID DERS_sc Avo LNB_sc Viv_Pos Viv_Neg CCxDERS;
CLUSTER = ID; 
BETWEEN = DERS_sc Avo LNB_sc CCxDERS;
DEFINE: CCxDERS = LNB_sc*DERS_sc;
ANALYSIS:
TYPE = TWOLEVEL;
ESTIMATOR = ML;
MODEL:
%WITHIN%    
Viv_Neg; !variance at the within-person level
Viv_Pos; !if you want to model means, use [] around the var name
%BETWEEN%     
Viv_Neg on Avo (b1) 
         DERS_sc (c1);
Viv_Pos on Avo (b2)
        DERS_sc (c2);
Avo on DERS_sc (a);       
Viv_Pos WITH Viv_Neg;   

!regress AVO on CC and the CC x DERS interaction
!this is the main effect of CC on AVO when DERS is at its mean 
!(as long as you z-scored or mean-centered DERS)
AVO on LNB_sc; 
!this is the interaction effect -- 
!indicates whether effect of DERS on AVO varies according to level of CC
AVO on CCxDERS;

MODEL CONSTRAINT:
NEW(pos_ind_viv pos_tot_viv neg_ind_viv neg_tot_viv dif_ind_viv dif_dir_viv);
pos_ind_viv = b2*a;
pos_tot_viv = b2*a + c2;
neg_ind_viv = b1*a;
neg_tot_viv = b1*a + c1;
dif_ind_viv = pos_ind_viv - neg_ind_viv;
dif_dir_viv = c2 - c1;

OUTPUT: 
stand residual;

