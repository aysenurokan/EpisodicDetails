!MSEM for Episodic (Internal) Details, 12/28/2022
title: Simple model looking at detail only;

DATA: FILE = "df_MPlus_12_28.dat";
VARIABLE: 
NAMES = ID trial DERS Avo AvoS LNB_MEFF LNB_log AMT_Pos AMT_Neg VP_Pos VP_Neg
     Rich_Pos Rich_Neg Det_Pos Det_Neg Viv_Pos Viv_Neg Int_Pos Int_Neg Ext_Pos
     Ext_Neg DERS_sq Avo_sq; 
MISSING=.;
USEVARIABLES = ID DERS Avo Int_Pos Int_Neg;
CLUSTER = ID; 
BETWEEN = DERS Avo;
ANALYSIS:
TYPE = TWOLEVEL;
ESTIMATOR = ML;
MODEL:
%WITHIN%    
Int_Neg; !variance at the within-person level
Int_Pos; !if you want to model means, use [] around the var name
%BETWEEN%     
Int_Neg on Avo (b1) 
         DERS (c1);
Int_Pos on Avo (b2)
        DERS (c2);
Avo on DERS (a);       
Int_Pos WITH Int_Neg;   

MODEL CONSTRAINT:
NEW(pos_ind_int pos_tot_int neg_ind_int neg_tot_int dif_ind_int);
pos_ind_int = b2*a;
pos_tot_int = b2*a + c2;
neg_ind_int = b1*a;
neg_tot_int = b1*a + c1;
dif_ind_int = pos_ind_int - neg_ind_int;

OUTPUT: 
stand residual;

