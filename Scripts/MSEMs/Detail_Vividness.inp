!MSEM for Episodic (Internal) details and Vividness, 12/29/2022
title: Model looking at detail and vividness;

DATA: 
FILE = "df_MPlus_12_29.dat";
VARIABLE:
NAMES = ID trial DERS Avo AvoS LNB_MEFF VP_Pos VP_Neg Rich_Pos Rich_Neg Det_Pos
     Det_Neg Viv_Pos Viv_Neg Int_Pos Int_Neg Ext_Pos Ext_Neg DERS_sc Avo_sc LNB_sc
     DERS_sq Avo_sq LNB_sq; 
USEVARIABLES = ID DERS Avo Int_Pos Int_Neg Viv_Pos Viv_Neg;
MISSING=.;
CLUSTER = ID; 
BETWEEN = DERS Avo;
ANALYSIS:
TYPE = TWOLEVEL;
ESTIMATOR = ML;
MODEL:
%WITHIN%    
Int_Neg; !variance at the within-person level
Int_Pos; !if you want to model means, use [] around the var name
Viv_Pos;
Viv_Neg;
%BETWEEN%     
Int_Neg on Avo (bd1) 
         DERS (cd1);
Int_Pos on Avo (bd2)
        DERS (cd2);
Viv_Neg on Avo (bv1) 
         DERS (cv1);
Viv_Pos on Avo (bv2)
        DERS (cv2);
Avo on DERS (a);       
Int_Pos WITH Int_Neg; !residual covariance
Viv_Pos WITH Viv_Neg;     
                
MODEL CONSTRAINT:
NEW(pos_ind_int pos_tot_int neg_ind_int neg_tot_int dif_ind_int 
	pos_ind_viv pos_tot_viv neg_ind_viv neg_tot_viv dif_ind_viv);
pos_ind_int = bd2*a;
pos_tot_int = bd2*a + cd2;
neg_ind_int = bd1*a;
neg_tot_int = bd1*a + cd1;
dif_ind_int = pos_ind_int - neg_ind_int;
pos_ind_viv = bv2*a;
pos_tot_viv = bv2*a + cv2;
neg_ind_viv = bv1*a;
neg_tot_viv = bv1*a + cv1;
dif_ind_viv = pos_ind_viv - neg_ind_viv;

OUTPUT: 
stand residual;
